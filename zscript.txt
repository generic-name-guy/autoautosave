version "3.2.5"

class m8f_aas_token : Inventory
{
  private int reason;

  Default
    {
      +Inventory.Quiet;
    }

  void set_reason(int r)
  {
    reason = r;
  }

  override bool TryPickup(Actor toucher)
  {
    m8f_aas_save_schedule s = m8f_aas_save_schedule.get();
    s.set_scheduled(reason);
    GoAwayAndDie();
    return true;
  }
}

class m8f_aas_save_schedule : Thinker
{
  // Reasons to save
  // less important first
  enum save_on
  {
    save_on_no_reason,

    // other
    save_on_all_kill,
    save_on_boss_kill,
    save_on_group_kill,

    save_on_group_alert,
    save_on_boss_alert,

    save_on_all_items_found,
    save_on_time_period,
    save_on_teleport, // 8

    // evident
    save_on_health_drop,
    save_on_health_rise,
    save_on_armor_drop,
    save_on_armor_rise,
    save_on_secret_found, // 13

    // pickups
    save_on_powerup,
    save_on_weapon,
    save_on_key,
    save_on_backpack,
    save_on_new_armor,
    save_on_big_heal, // 19

    end_of_reasons,
  };

  private static string message(save_on reason)
  {
    static const string messages[] =
      {
        "Absolutely no reason to save.",

        // other
        "All enemies are eliminated.",
        "Boss is eliminated.",
        "No more active enemies.",

        "New active enemies.",
        "Boss is alerted.",

        "All items are found.",
        "Time has passed.",
        "You are moved to another place.",

        // evident
        "Health drops low.",
        "Health rises high.",
        "Armor drops low.",
        "Armor rises high.",
        "Secret is found.",

        // pickups
        "Powerup is found.",
        "Weapon is found.",
        "Key is found.",
        "Backpack is found.",
        "New armor type.",
        "Major healing.",

        "Autosave on unknown reason"
      };

    if (reason > end_of_reasons) { reason = end_of_reasons; }

    return messages[reason];
  }

  private static bool saveEnabled(save_on reason)
  {
    static const string toggles[] =
      {
        "m8f_aas_save_on_all_kill",
        "m8f_aas_save_on_boss_kill",
        "m8f_aas_save_on_group_kill",

        "m8f_aas_save_on_group_alert",
        "m8f_aas_save_on_boss_alert",

        "m8f_aas_save_on_all_items_found",
        "m8f_aas_save_on_time_period",
        "m8f_aas_save_on_teleport",

        "m8f_aas_save_on_health_drop",
        "m8f_aas_save_on_health_rise",
        "m8f_aas_save_on_armor_drop",
        "m8f_aas_save_on_armor_rise",
        "m8f_aas_save_on_secret_found",

        "m8f_aas_save_on_powerup",
        "m8f_aas_save_on_weapon",
        "m8f_aas_save_on_key",
        "m8f_aas_save_on_backpack",
        "m8f_aas_save_on_new_armor",
        "m8f_aas_save_on_big_heal"
      };

    if (CVar.GetCVar("m8f_aas_enabled").GetInt() == false) { return false; }

    int health = players[consoleplayer].Health;
    int min_health = CVar.GetCVar("m8f_aas_health_limit").GetInt();
    if (health < min_health) { return false; }

    if (reason <= 0 || end_of_reasons <= reason) { return false; }

    return CVar.GetCVar(toggles[reason - 1]).GetInt();
  }

  private save_on reason;

  m8f_aas_save_schedule Init()
  {
    reason = save_on_no_reason;
    ChangeStatNum(STAT_INFO);
    return self;
  }

  static m8f_aas_save_schedule get()
  {
    ThinkerIterator it = ThinkerIterator.Create("m8f_aas_save_schedule", STAT_INFO);
    let p = m8f_aas_save_schedule(it.Next());
    if (p == null)
      {
        p = new("m8f_aas_save_schedule").Init();
      }
    return p;
  }

  void set_scheduled(save_on r)
  {
    reason = r;
  }

  int get_scheduled()
  {
    return reason;
  }

  bool save()
  {
    if (reason <= m8f_aas_save_schedule.save_on_no_reason ||
        reason >= m8f_aas_save_schedule.end_of_reasons) { return false; }

    int console_level = CVar.GetCVar("m8f_aas_console_log_level").GetInt();
    if (reason <= console_level)
      {
        Console.Printf(message(reason));
      }

    int screen_level = CVar.GetCVar("m8f_aas_screen_level").GetInt();
    if (reason <= screen_level)
      {
        Console.MidPrint("smallfont", message(reason), true);
      }

    int voice_level = CVar.GetCVar("m8f_aas_voice_level").GetInt();
    if (reason <= voice_level)
      {
        string voice_file = String.Format("aas/voice%d", reason);
        Object.S_Sound(voice_file, 0);
      }

    if (!saveEnabled(reason)) { return false; }

    players[consoleplayer].mo.ACS_NamedExecute("m8f_aas_save", 0);

    return true;
  }

}

class m8f_aas_event_handler : EventHandler
{
  int tick;
  bool loading_finished;
  bool first_tick;
  const ticksInSecond = 35;

  int oldActiveCount;
  int oldActiveBigCount;
  int maxActive;

  int old_kill_count;
  int old_item_count;
  int old_secret_count;

  vector3 old_pos;

  int old_health;
  int old_armor;
  double old_armor_save;

  int seconds_from_last_save;

  override void OnRegister()
  {
    tick = 0;
    first_tick = true;
    loading_finished = false;

    oldActiveCount = 0;
    oldActiveBigCount = 0;
    maxActive = 0;

    old_kill_count = 0;
    old_item_count = 0;
    old_secret_count = 0;
  }

  override void WorldTick()
  {
    if (first_tick) { first_tick = !first_tick; return; }

    loading_finished = true;

    if (tick < ticksInSecond) { ++tick; return; }

    tick = 0;
    ++seconds_from_last_save;

    m8f_aas_save_schedule schedule = m8f_aas_save_schedule.get();
    check_time_save(schedule);
    check_counter_events(schedule);
    check_map_events(schedule);
    check_player_events(schedule);

    int min_save_wait = CVar.GetCVar("m8f_aas_min_save_wait").GetInt();
    if (seconds_from_last_save < min_save_wait) { return; }

    bool saved = schedule.save();
    schedule.set_scheduled(m8f_aas_save_schedule.save_on_no_reason);
    if (saved) { seconds_from_last_save = 0; }
  }

  void check_time_save(m8f_aas_save_schedule schedule)
  {
    int autosave_period = CVar.GetCVar("m8f_aas_autosave_period").GetInt();
    if (seconds_from_last_save >= autosave_period)
      {
        schedule.set_scheduled(m8f_aas_save_schedule.save_on_time_period);
      }
  }

  void check_map_events(m8f_aas_save_schedule schedule)
  {
    PlayerInfo player = players[consoleplayer];

    {
      int secret_count = player.secretcount;
      if (secret_count > old_secret_count)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_secret_found);
        }
      old_secret_count = secret_count;
    }

    {
      int kill_count = level.killed_monsters;
      if (kill_count != old_kill_count && kill_count == level.total_monsters)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_all_kill);
        }
      old_kill_count = kill_count;
    }

    {
      int item_count = level.found_items;
      if (item_count != old_item_count && item_count == level.total_items)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_all_items_found);
        }
      old_item_count = item_count;
    }
  }

  void check_player_events(m8f_aas_save_schedule schedule)
  {
    PlayerInfo player = players[consoleplayer];

    {
      vector3 pos = player.mo.Pos;
      float x_diff = (pos.x - old_pos.x);
      float y_diff = (pos.y - old_pos.y);
      float dist = x_diff * x_diff + y_diff * y_diff;
      //Console.Printf("Distance: %f", dist);
      if (dist > 1000000.0)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_teleport);
        }
      old_pos = pos;
    }

    {
      int health      = player.mo.health;
      int health_down = CVar.GetCVar("m8f_aas_health_threshold_down").GetInt();
      int health_up   = CVar.GetCVar("m8f_aas_health_threshold_up").GetInt();
      if (health < health_down && old_health >= health_down)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_health_drop);
        }
      else if (health > health_up && old_health <= health_up)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_health_rise);
        }
      else if (health >= old_health + 50)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_big_heal);
        }
      old_health = health;
    }

    {
      int armor_count = player.mo.CountInv("BasicArmor");
      int armor_down  = CVar.GetCVar("m8f_aas_armor_threshold_down").GetInt();
      int armor_up    = CVar.GetCVar("m8f_aas_armor_threshold_up").GetInt();
      if (armor_count < armor_down && old_armor >= armor_down)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_armor_drop);
        }
      else if (armor_count > armor_up && old_armor <= armor_up)
        {
          schedule.set_scheduled(m8f_aas_save_schedule.save_on_armor_rise);
        }
      old_armor = armor_count;
    }

    {
      BasicArmor armor = BasicArmor(player.mo.FindInventory("BasicArmor"));
      if (armor != null)
        {
          double save_percent = armor.SavePercent;
          if (save_percent != 0.0 && save_percent != old_armor_save)
            {
              schedule.set_scheduled(m8f_aas_save_schedule.save_on_new_armor);
            }
          old_armor_save = save_percent;
        }
    }
  }

  void check_counter_events(m8f_aas_save_schedule schedule)
  {
    // count active monsters
    ThinkerIterator i   = ThinkerIterator.Create("Actor", Thinker.STAT_DEFAULT);
    int activeCount     = 0;
    int activeBigCount  = 0;
    int min_boss_health = CVar.GetCVar("m8f_aas_min_boss_health").GetInt();
    Actor a;

    while (a = Actor(i.Next()))
      {
        if (a.bISMONSTER
            && a.Target != null
            && a.Health > 0)
          {
            if (a.SpawnHealth() >= min_boss_health) { ++activeBigCount; }
            ++activeCount;
          }
      }
    //Console.Printf("Counts: %d, %d", activeCount, activeBigCount);

    if (activeCount > maxActive) { maxActive = activeCount; }

    int group_number = CVar.GetCVar("m8f_aas_group_number").GetInt();
    if (activeCount >= oldActiveCount + group_number)
      {
        schedule.set_scheduled(m8f_aas_save_schedule.save_on_group_alert);
      }
    else if (activeCount == 0)
      {
        if (maxActive >= group_number)
          {
            schedule.set_scheduled(m8f_aas_save_schedule.save_on_group_kill);
          }
        maxActive = 0;
      }
    else if (activeBigCount > oldActiveBigCount)
      {
        schedule.set_scheduled(m8f_aas_save_schedule.save_on_boss_alert);
      }
    else if (activeBigCount < oldActiveBigCount)
      {
        schedule.set_scheduled(m8f_aas_save_schedule.save_on_boss_kill);
      }

    oldActiveCount = activeCount;
    oldActiveBigCount = activeBigCount;
  }

  override void WorldThingSpawned(WorldEvent e)
  {
    // spawn special actor that saves the game when picked up
    // alongside inventory items.

    if (e.thing.Player == players[consoleplayer])
      {
        old_pos    = e.thing.Pos;
        old_health = e.thing.Health;
        old_armor  = e.thing.CountInv("BasicArmor");

        BasicArmor armor = BasicArmor(e.thing.FindInventory("BasicArmor"));
        if (armor) { old_armor_save = armor.SavePercent; }
        else       { old_armor_save = 0.0; }
      }

    Inventory item = Inventory(e.thing);
    if (item == null) { return; }

    bool saveOnDropped = CVar.GetCVar("m8f_aas_save_on_dropped").GetInt();
    if (!saveOnDropped && loading_finished) { return; }

    static const string saveable_item_classes[] =
      {
        "Key",

        "Weapon",
        "Goonades",

        "PowerupGiver",
        "TBPowerupBase",
        "MapRevealer",
        "Berserk",

        "BackpackItem"
      };
    static const int reasons[] =
      {
        m8f_aas_save_schedule.save_on_key,

        m8f_aas_save_schedule.save_on_weapon,
        m8f_aas_save_schedule.save_on_weapon,

        m8f_aas_save_schedule.save_on_powerup,
        m8f_aas_save_schedule.save_on_powerup,
        m8f_aas_save_schedule.save_on_powerup,
        m8f_aas_save_schedule.save_on_powerup,

        m8f_aas_save_schedule.save_on_backpack
      };
    int n_saveable_items_classes = 8;

    int reason = m8f_aas_save_schedule.save_on_no_reason;
    for (int i = 0; i < n_saveable_items_classes; ++i)
      {
        if (item is saveable_item_classes[i])
          {
            reason = reasons[i];
            m8f_aas_token token;
            Actor owner = item.owner;
            if (owner == null)
              {
                token = m8f_aas_token(Actor.Spawn("m8f_aas_token", item.SpawnPoint));
              }
            else if (loading_finished) // don't save on obtaining starting weapons
              {
                token = m8f_aas_token(Actor.Spawn("m8f_aas_token"));
                owner.AddInventory(token);
              }
            token.set_reason(reason);
            break;
          }
      }
  }
}
