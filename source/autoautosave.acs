#library "Autoautosave"
#include "zcommon.acs"

int timeCounter = 0;
int health = 0;

int minBossHealth;
int saveMinWait;
int healthLimit;
bool enabled;

bool massiveAlertEnabled;
bool bossAlertEnabled;
int alertLimit;
int alertCountOld = 0;

script "m8f_AasToggle" (void)
{
  int playerN = PlayerNumber();
  enabled = GetUserCVar(playerN, "M8fAasEnabled");
  enabled = !enabled;
  SetUserCVar(playerN, "M8fAasEnabled", enabled);
  if (enabled)
  {
    Log(s:"Autoautosave is on.");
  }
  else
  {
    Log(s:"Autoautosave is off.");
  }
}

script "m8f_ManualAutoSave" (void)
{
  Autosave();
  timeCounter = 0;
}

script "m8f_WeaponPickupAutoSave" (void)
{
  bool autosaveOnWeapon = GetCVar("M8fAasSaveOnWeapon");

  if (enabled
     && autosaveOnWeapon
     && timeCounter > saveMinWait
     && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_KeyPickupAutoSave" (void)
{
  bool autosaveOnKey = GetCVar("M8fAasSaveOnKeys");

  if (enabled && autosaveOnKey && timeCounter > saveMinWait && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_PowerupPickupAutoSave" (void)
{
  bool autosaveOnPowerup = GetCVar("M8fAasSaveOnPowerup");

  if (enabled && autosaveOnPowerup && timeCounter > saveMinWait && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_BackpackPickupAutoSave" (void)
{
  bool autosaveOnBackpack = GetCVar("M8fAasSaveOnBackpack");

  if (enabled && autosaveOnBackpack && timeCounter > saveMinWait && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_AlertSave" (void)
{
  if (!massiveAlertEnabled || !enabled) { terminate; }

  int alertCount = ThingCountName("AutoautosaveAlertToken", 0) - alertCountOld;
  alertCountOld = alertCount;

  if (alertCount >= alertLimit && timeCounter > saveMinWait && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_BossAlertSave" (void)
{
  if (!bossAlertEnabled || !enabled) { terminate; }

  if (timeCounter > saveMinWait && health >= healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_CheckKill" Kill
{
  SetCVar("M8fAasMapLoaded", true);

  int maxHealth = GetActorProperty(0, APROP_SpawnHealth);
  if (maxHealth >= minBossHealth
      && timeCounter > saveMinWait
      && health >= healthLimit
      && enabled)
  {
    Log(s:"Boss killed!");
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_Autoautosave" Enter
{
  bool allMonstersKilledOld = AS_IsAllMonsterKilled();
  bool allItemsFoundOld     = AS_IsAllItemsFound();
  int  secretsFoundOld      = AS_SecretsFound();

  str  armorOld             = AS_GetArmor();
  int  healthOld            = AS_GetHealth();
  int  armorValueOld        = AS_GetArmorValue();

  int  xOld = AS_GetX();
  int  yOld = AS_GetY();

  int playerN = PlayerNumber();

  while (true)
  {
    enabled = GetUserCVar(playerN, "M8fAasEnabled");

    saveMinWait   = GetUserCVar(playerN, "M8fAasMinSaveWait")   >> 16;
    minBossHealth = GetCVar("M8fAasMinBossHealth") >> 16;
    healthLimit   = GetUserCVar(playerN, "M8fAasHealthLimit")   >> 16;
    int autosavePeriod = (GetUserCVar(playerN, "M8fAasAutosavePeriod") >> 16) * 60;

    alertLimit          = GetUserCVar(playerN, "M8fAasMassiveAlertThreshold") >> 16;
    massiveAlertEnabled = GetUserCVar(playerN, "M8fAasSaveOnMassiveAlert");
    bossAlertEnabled    = GetUserCVar(playerN, "M8fAasSaveOnBossAlert");

    int healthThresholdDown = GetUserCVar(playerN, "M8fAasHealthThresholdDown") >> 16;
    int healthThresholdUp   = GetUserCVar(playerN, "M8fAasHealthThresholdUp")   >> 16;
    int armorThresholdDown  = GetUserCVar(playerN, "M8fAasArmorThresholdDown")  >> 16;
    int armorThresholdUp    = GetUserCVar(playerN, "M8fAasArmorThresholdUp")    >> 16;

    bool saveOnBigHeal     = GetUserCVar(playerN, "M8fAasSaveOnBigHeal");
    bool saveOnArmorChange = GetUserCVar(playerN, "M8fAasSaveOnArmorChange");
    bool saveOnAllKilled   = GetUserCVar(playerN, "M8fAasSaveOnAllKilled");
    bool saveOnAllFound    = GetUserCVar(playerN, "M8fAasSaveOnAllFound");
    bool saveOnSecret      = GetUserCVar(playerN, "M8fAasSaveOnSecret");
    bool saveOnTeleport    = GetUserCVar(playerN, "M8fAasSaveOnTeleport");

    bool saveOnHealthDrop = GetUserCVar(playerN, "M8fAasSaveOnHealthDrop");
    bool saveOnHealthRise = GetUserCVar(playerN, "M8fAasSaveOnHealthRise");
    bool saveOnArmorDrop  = GetUserCVar(playerN, "M8fAasSaveOnArmorDrop");
    bool saveOnArmorRise  = GetUserCVar(playerN, "M8fAasSaveOnArmorRise");

    ++timeCounter;

    health         = AS_GetHealth();
    int armorValue = AS_GetArmorValue();
    str armor      = AS_GetArmor();

    bool allMonstersKilled = AS_IsAllMonsterKilled();
    bool allItemsFound     = AS_IsAllItemsFound();
    int  secretsFound      = AS_SecretsFound();

    int x = AS_GetX();
    int y = AS_GetY();
    int deltaX = x - xOld;
    int deltaY = y - yOld;
    int dist = deltaX * deltaX + deltaY * deltaY;
    int maxRunDist = 2500000;

    bool mustAutosave = (saveOnBigHeal && health > healthOld + 25)
      || (saveOnArmorChange && armorOld != armor && StrCmp(armor, "None"))

      || (saveOnAllKilled && allMonstersKilledOld != allMonstersKilled)
      || (saveOnAllFound && allItemsFoundOld != allItemsFound)
      || (saveOnSecret && secretsFoundOld != secretsFound)
      || (saveOnTeleport   && dist > maxRunDist)

      || (autosavePeriod != 0 && timeCounter >= autosavePeriod)

      || (saveOnHealthDrop && healthOld >= healthThresholdDown && health < healthThresholdDown)
      || (saveOnHealthRise && healthOld <= healthThresholdUp   && health > healthThresholdUp)
      || (saveOnArmorDrop  && armorValueOld >= armorThresholdDown && armorValue < armorThresholdDown)
      || (saveOnArmorRise  && armorValueOld <= armorThresholdUp   && armorValue > armorThresholdUp)
      ;

    healthOld = health;
    armorOld  = armor;
    armorValueOld = armorValue;

    allMonstersKilledOld = allMonstersKilled;
    allItemsFoundOld     = allItemsFound;
    secretsFoundOld      = secretsFound;

    xOld = x;
    yOld = y;

    if (mustAutosave
        && timeCounter > saveMinWait
        && health >= healthLimit
        && enabled
        )
    {
      Autosave();
      timeCounter = 0;
    }

    Delay(35);
  }
}

function bool AS_IsAllMonsterKilled(void)
{
  int totalMonsters  = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
  int killedMonsters = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
  return (totalMonsters == killedMonsters);
}

function bool AS_IsAllItemsFound(void)
{
  int totalItems = GetLevelInfo(LEVELINFO_TOTAL_ITEMS);
  int foundItems = GetLevelInfo(LEVELINFO_FOUND_ITEMS);
  return (totalItems == foundItems);
}

function int AS_SecretsFound(void)
{
  return GetLevelInfo(LEVELINFO_FOUND_SECRETS);
}

function str AS_GetArmor(void)
{
  return GetArmorInfo(ARMORINFO_CLASSNAME);
}

function str AS_GetHealth(void)
{
  return GetActorProperty(0, APROP_HEALTH);
}

function str AS_GetArmorValue(void)
{
  return CheckInventory("BasicArmor");
}

function int AS_GetX(void)
{
  return GetActorX(0) >> 16;
}

function int AS_GetY(void)
{
  return GetActorY(0) >> 16;
}
