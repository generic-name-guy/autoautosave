#library "Autoautosave"
#include "zcommon.acs"

int timeCounter = 0;
int health = 0;

script "m8f_ManualAutoSave" (void)
{
  Autosave();
  timeCounter = 0;
}

script "m8f_CheckKill" Kill
{
  int saveMinWait = 30;
  int healthLimit = 24;
  int maxHealth = GetActorProperty(0, APROP_SpawnHealth);
  if (maxHealth >= 2000 && timeCounter > saveMinWait && health > healthLimit)
  {
    Autosave();
    timeCounter = 0;
  }
}

script "m8f_Autoautosave" Enter
{
  bool allMonstersKilledOld = AS_IsAllMonsterKilled();
  bool allItemsFoundOld     = AS_IsAllItemsFound();
  int  secretsFoundOld      = AS_SecretsFound();

  str  armorOld             = AS_GetArmor();
  int  healthOld            = AS_GetHealth();

  int  keyCountOld          = AS_GetKeyCount();
  int  weaponCountOld       = AS_GetWeaponCount();
  int  powerupCountOld      = AS_GetPowerupCount();

  int saveMinWait = 30;
  int autosavePeriod = 5 * 60; // 5 minutes
  int healthLimit = 24;

  while (true)
  {
    ++timeCounter;
    bool mustAutosave = false;

    health    = AS_GetHealth();
    str armor = AS_GetArmor();

    bool allMonstersKilled = AS_IsAllMonsterKilled();
    bool allItemsFound     = AS_IsAllItemsFound();
    int  secretsFound      = AS_SecretsFound();

    int keyCount     = AS_GetKeyCount();
    int weaponCount  = AS_GetWeaponCount();
    int powerupCount = AS_GetPowerupCount();

    mustAutosave =
         (health > healthOld + 25) // heal more than 25? You found something good!
      || (armorOld != armor && StrCmp(armor, "None"))

      || (allMonstersKilledOld != allMonstersKilled)
      || (allItemsFoundOld != allItemsFound)
      || (secretsFoundOld != secretsFound)

      || (timeCounter >= autosavePeriod)

      || (keyCount > keyCountOld)
      || (weaponCount > weaponCountOld)
      || (powerupCount > powerupCountOld)
      ;

    healthOld = health;
    armorOld  = armor;

    allMonstersKilledOld = allMonstersKilled;
    allItemsFoundOld     = allItemsFound;
    secretsFoundOld      = secretsFound;

    keyCountOld     = keyCount;
    weaponCountOld  = weaponCount;
    powerupCountOld = powerupCount;

    if (mustAutosave && timeCounter > saveMinWait && health > healthLimit)
    {
      Autosave();
      timeCounter = 0;
    }

    Delay(35);
  }
}

function bool AS_IsAllMonsterKilled(void)
{
  int totalMonsters  = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
  int killedMonsters = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
  return (totalMonsters == killedMonsters);
}

function bool AS_IsAllItemsFound(void)
{
  int totalItems = GetLevelInfo(LEVELINFO_TOTAL_ITEMS);
  int foundItems = GetLevelInfo(LEVELINFO_FOUND_ITEMS);
  return (totalItems == foundItems);
}

function int AS_SecretsFound(void)
{
  return GetLevelInfo(LEVELINFO_FOUND_SECRETS);
}

function str AS_GetArmor(void)
{
  return GetArmorInfo(ARMORINFO_CLASSNAME);
}

function str AS_GetHealth(void)
{
  return GetActorProperty(0, APROP_HEALTH);
}

function int AS_GetKeyCount(void)
{
  return CheckInventory("AutoautosaveTokenKey");
}

function int AS_GetWeaponCount(void)
{
  return CheckInventory("AutoautosaveTokenWeapon");
}

function int AS_GetPowerupCount(void)
{
  return CheckInventory("AutoautosaveTokenPowerup");
}
