#library "Autosaver2"
#include "zcommon.acs"

script "m8f_Autosaver2" Enter
{
  int timeCounter = 0;

  bool allMonstersKilledOld = AS_IsAllMonsterKilled();
  bool allItemsFoundOld     = AS_IsAllItemsFound();
  int  secretsFoundOld      = AS_SecretsFound();
  str  armorOld             = AS_GetArmor();

  int saveMinWait = 1;

  while (true)
  {
    ++timeCounter;

    str armor = AS_GetArmor();
    if (armorOld != armor)
    {
      armorOld = armor;
      if (timeCounter > saveMinWait)
      {
        Autosave();
        timeCounter = 0;
      }
      Delay(35);
      continue;
    }

    bool allMonstersKilled = AS_IsAllMonsterKilled();
    if (allMonstersKilledOld != allMonstersKilled)
    {
      allMonstersKilledOld = allMonstersKilled;
      if (timeCounter > saveMinWait)
      {
        AS_AutosaveWithMessage("Level clear.");
        timeCounter = 0;
      }
      Delay(35);
      continue;
    }

    bool allItemsFound = AS_IsAllItemsFound();
    if (allItemsFoundOld != allItemsFound)
    {
      allItemsFoundOld = allItemsFound;
      if (timeCounter > saveMinWait)
      {
        AS_AutosaveWithMessage("All items found.");
        timeCounter = 0;
      }
      Delay(35);
      continue;
    }

    int secretsFound = AS_SecretsFound();
    if (secretsFoundOld != secretsFound)
    {
      secretsFoundOld = secretsFound;
      if (timeCounter > saveMinWait)
      {
        Autosave();
        timeCounter = 0;
      }
      Delay(35);
      continue;
    }

    Delay(35);
  }
}

function void AS_AutosaveWithMessage(str message)
{
  Log(s:message);
  Autosave();
}

function bool AS_IsAllMonsterKilled(void)
{
  int totalMonsters  = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
  int killedMonsters = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
  return (totalMonsters == killedMonsters);
}

function bool AS_IsAllItemsFound(void)
{
  int totalItems = GetLevelInfo(LEVELINFO_TOTAL_ITEMS);
  int foundItems = GetLevelInfo(LEVELINFO_FOUND_ITEMS);
  return (totalItems == foundItems);
}

function int AS_SecretsFound(void)
{
  return GetLevelInfo(LEVELINFO_FOUND_SECRETS);
}

function str AS_GetArmor(void)
{
  return GetArmorInfo(ARMORINFO_CLASSNAME);
}
