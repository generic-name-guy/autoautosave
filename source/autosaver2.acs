#library "Autosaver2"
#include "zcommon.acs"

script "m8f_Autosaver2" Enter
{
  int timeCounter = 0;

  bool allMonstersKilledOld = AS_IsAllMonsterKilled();
  bool allItemsFoundOld     = AS_IsAllItemsFound();
  int  secretsFoundOld      = AS_SecretsFound();
  str  armorOld             = AS_GetArmor();
  int  healthOld            = AS_GetHealth();
  bool radSuitActiveOld     = AS_IsRadSuitActive();
  int  cyberdemonCountOld   = AS_GetMonsterCount("Cyberdemon");
  int  spidermasterCountOld = AS_GetMonsterCount("SpiderMastermind");

  int saveMinWait = 1;
  int autosavePeriod = 5 * 60; // 5 minutes
  int healthLimit = 25;

  while (true)
  {
    ++timeCounter;
    bool mustAutosave = false;

    int radSuitActive = AS_IsRadSuitActive();
    int health = AS_GetHealth();
    str armor = AS_GetArmor();
    bool allMonstersKilled = AS_IsAllMonsterKilled();
    bool allItemsFound = AS_IsAllItemsFound();
    int secretsFound = AS_SecretsFound();
    int cyberDemonCount = AS_GetMonsterCount("Cyberdemon");
    int spidermasterCount = AS_GetMonsterCount("SpiderMastermind");

    mustAutosave = (radSuitActive && radSuitActive != radSuitActiveOld)
      || (health > healthOld + 25) // heal more than 25? You found something good!
      || (armorOld != armor)
      || (allMonstersKilledOld != allMonstersKilled)
      || (allItemsFoundOld != allItemsFound)
      || (secretsFoundOld != secretsFound)
      || (timeCounter >= autosavePeriod)
      || (cyberdemonCountOld > cyberdemonCount)
      || (spidermasterCountOld > spidermasterCount)
      ;

    radSuitActiveOld = radSuitActive;
    healthOld = health;
    armorOld = armor;
    allMonstersKilledOld = allMonstersKilled;
    allItemsFoundOld = allItemsFound;
    secretsFoundOld = secretsFound;
    cyberdemonCountOld = cyberdemonCount;
    spidermasterCountOld = spidermasterCount;

    if (mustAutosave && timeCounter > saveMinWait && health > healthLimit)
    {
      Autosave();
      timeCounter = 0;
    }

    Delay(35);
  }
}

function void AS_AutosaveWithMessage(str message)
{
  Log(s:message);
  Autosave();
}

function bool AS_IsAllMonsterKilled(void)
{
  int totalMonsters  = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
  int killedMonsters = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
  return (totalMonsters == killedMonsters);
}

function bool AS_IsAllItemsFound(void)
{
  int totalItems = GetLevelInfo(LEVELINFO_TOTAL_ITEMS);
  int foundItems = GetLevelInfo(LEVELINFO_FOUND_ITEMS);
  return (totalItems == foundItems);
}

function int AS_SecretsFound(void)
{
  return GetLevelInfo(LEVELINFO_FOUND_SECRETS);
}

function str AS_GetArmor(void)
{
  return GetArmorInfo(ARMORINFO_CLASSNAME);
}

function str AS_GetHealth(void)
{
  return GetActorProperty(0, APROP_HEALTH);
}

function bool AS_IsRadSuitActive(void)
{
  return (GetActorPowerupTics(0, "PowerIronFeet") != 0);
}

function int AS_GetMonsterCount(str monster)
{
  return ThingCountName(monster, 0);
}
